import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.ajoberstar.grgit:grgit-gradle:3.1.1"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.22.0" apply false
    id "com.gradle.build-scan" version "2.4"
}

apply plugin: 'application'

ext {
    grgit = Grgit.open(dir: "${rootProject.projectDir}")
    localGitCommit = grgit.head().id
    localGitCommitShort = grgit.head().getAbbreviatedId(7)
    localGitDirty = !grgit.status().clean

    // Dependencies versions
    annotations = '17.0.0'
    antlr = '4.7.2'
    apacheCommonsCompress = '1.18'
    apacheCommonsCsv = '1.7'
    apacheCommonsText = '1.7'
    asm = '7.1'
    commonsCli = '1.4'
    discord = '1.1'
    fernflower = '20171017'
    findbugs = '3.0.2'
    gson = '2.8.5'
    guava = '28.0-jre'
    guice = '4.2.2'
    h2 = '1.4.199'
    hamcrest = '2.1'
    httpcore = '4.4.11'
    httpmime = '4.5.9'
    javassist = '3.25.0-GA'
    javaxInject = '1'
    jbsdiff = '1.0'
    jclCore = '2.8'
    jedis = '3.1.0'
    jna = '5.4.0'
    jogamp = '2.3.2'
    jopt = '5.0.4'
    jooq = '3.11.12'
    junit = '4.12'
    jupiter = '5.5.1'
    logback = '1.2.3'
    lombok = '1.18.8'
    mapstruct = '1.3.0.Final'
    mariadbJdbc = '2.4.3'
    mavenPluginAnnotations = '3.6.0'
    mavenPluginApi = '3.6.1'
    minio = '6.0.9'
    mockito = '3.0.0'
    mongodbDriverSync = '3.11.0'
    mysqlConnectorJava = '8.0.17'
    netty = '4.1.37.Final'
    okhttp3 = '4.1.0'
    orangeExtensions = '1.0'
    petitparser = '2.2.0'
    plexus = '3.2.1'
    rxjava = '2.2.11'
    rxrelay = '2.1.0'
    scribejava = '6.7.0'
    sisu = '0.3.3'
    slf4j = '1.7.26'
    springJdbc = '5.1.9.RELEASE'
    springboot = '2.1.7.RELEASE'
    sql2o = '1.6.0'
    substance = '8.0.02'
    trident = '1.5.00'
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'checkstyle'

    group = 'net.runelite'
    version = '1.5.32-SNAPSHOT'

    ext {
        rsversion = 181
        cacheversion = 165
        plusVersion = '2.1.4.0'

        gitCommit = localGitCommit
        gitCommitShort = localGitCommitShort
        gitDirty = localGitDirty

        rootPath = rootDir.toString().replace('\\', '/')
        injectedClassesPath = rootPath + "/injector-plugin/out/injected-client/"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: "com.github.ben-manes.versions"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(GroovyCompile).configureEach {
        options.incremental = true
    }

    repositories {
        mavenLocal()

        maven { url "http://repo1.maven.org/maven2" }
        maven { url "http://repo.runelite.net" }
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "https://raw.githubusercontent.com/runelite-extended/maven-repo/master" }

        if (System.getenv("NEXUS-URL") != null) {
            maven { url System.getenv("NEXUS-URL") }
        }
    }

    checkstyle {
        toolVersion = '6.4.1'
        sourceSets = [sourceSets.main]
        configFile = rootProject.file("./checkstyle/checkstyle.xml")
        configProperties = [ "suppressionFile" : rootProject.file("./checkstyle/suppressions.xml")]
        showViolations = true
        ignoreFailures = false
        maxWarnings = 0
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: System.getenv("NEXUS_URL")) {
                    authentication(userName: System.getenv("NEXUS_USER"), password: System.getenv("NEXUS_PASSWORD"))
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '5.6'

    doLast {
        def optsEnvVar = "DEFAULT_JVM_OPTS"
        scriptFile.write scriptFile.text.replace("$optsEnvVar='\"-Xmx64m\" \"-Xms64m\"'", "$optsEnvVar='\"-Xmx4g\" \"-Xms2g\" \"-Dfile.encoding=UTF-8\"'")
        batchScript.write batchScript.text.replace("set $optsEnvVar=\"-Xmx64m\" \"-Xms64m\"", "set $optsEnvVar=\"-Xmx4g\" \"-Xms2g\" \"-Dfile.encoding=UTF-8\"")
    }
}

run {
    classpath = childProjects.client.sourceSets.main.runtimeClasspath
    mainClassName = "net.runelite.client.RuneLite"
}
